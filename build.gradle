plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'org.owasp.dependencycheck' version '9.0.9'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'io.github.devatherock'
description = 'java.util.logging.Formatter to log messages as JSON, compatible with Logstash'
version = (System.env.CIRCLE_TAG ? System.env.CIRCLE_TAG.substring(1): '1.3.0') +
        (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")

repositories {
    mavenCentral()
}

ext {
    jsonSimpleVersion = '1.1.1'
    gsonVersion = '2.10.1'
    jacksonVersion = '2.16.1'

    // Properties for publishing
    licenseUrl = 'https://github.com/devatherock/jul-jsonformatter/blob/master/LICENSE.txt'
    scmUrl = 'https://github.com/devatherock/jul-jsonformatter/'
}

configurations {
    // Force dependency versions to use dependencies without vulnerabilities
    all {
        resolutionStrategy {
            force 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
        }
    }

    // Configurations for tests
    jsonSimpleTestImplementation.extendsFrom testImplementation
    jsonSimpleTestCompileOnly.extendsFrom compileOnly
    gsonTestImplementation.extendsFrom testImplementation
    gsonTestCompileOnly.extendsFrom compileOnly
    jacksonTestImplementation.extendsFrom testImplementation
    jacksonTestCompileOnly.extendsFrom compileOnly
    customJsonTestImplementation.extendsFrom testImplementation
    customJsonTestCompileOnly.extendsFrom compileOnly
}

dependencies {
    compileOnly group: 'com.googlecode.json-simple', name: 'json-simple', version: jsonSimpleVersion
    compileOnly group: 'com.google.code.gson', name: 'gson', version: gsonVersion
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.10.1'

    // Test configuration dependencies
    jsonSimpleTestImplementation group: 'com.googlecode.json-simple', name: 'json-simple', version: jsonSimpleVersion
    gsonTestImplementation group: 'com.google.code.gson', name: 'gson', version: gsonVersion
    jacksonTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
}

// sourceSets for tests
def allTestFiles = [
    '**/JSONSimpleFormatterTest*',
    '**/GSONFormatterTest*',
    '**/JacksonFormatterTest*',
    '**/CustomJsonConverterTest*',
    '**/CustomKeysTest*'
]
def inclusionsBySourceSet = [
    'jsonSimpleTest': [
        '**/JSONSimpleFormatterTest*'
    ],
    'gsonTest': ['**/GSONFormatterTest*'],
    'jacksonTest': [
        '**/JacksonFormatterTest*',
        '**/CustomKeysTest*'
    ],
    'customJsonTest': [
        '**/CustomJsonConverterTest*'
    ],
]

sourceSets {
    inclusionsBySourceSet.each { name, inclusions ->
        sourceSets.create(name) {
            java {
                srcDirs = [
                    "$projectDir/src/main/java",
                    "$projectDir/src/test/java"
                ]
                allTestFiles.findAll { !inclusions.contains(it) }.each { testFile ->
                    exclude testFile
                }
            }
            resources.srcDir file('src/test/resources')
        }
    }
}

/** Test tasks **/
def testDescriptions = [
    'json-simple',
    'gson',
    'jackson',
    'custom JSON'
]
def testSourceSetNames = [
    'jsonSimpleTest',
    'gsonTest',
    'jacksonTest',
    'customJsonTest'
]

for(int index = 0; index < testSourceSetNames.size(); index++) {
    tasks.create(name: testSourceSetNames[index], type: Test) {
        description = "Test ${testDescriptions}"
        testClassesDirs = project.sourceSets[testSourceSetNames.get(index)].output.classesDirs
        classpath = project.sourceSets[testSourceSetNames.get(index)].runtimeClasspath

        // Prevent JUnit5 from scanning for tests in non-test classes
        include(inclusionsBySourceSet[testSourceSetNames[index]])
    }
}

apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/publish.gradle'
apply from: 'checks.gradle'
apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/non-test-checks.gradle'

compileTestJava.enabled = false