task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name 'MIT License'
            url 'https://github.com/devatherock/jul-jsonformatter/blob/master/LICENSE.txt'
        }
    }

    developers {
        developer {
            id 'devatherock'
            name 'Devaprasadh Xavier'
            email 'devatherock@gmail.com'
        }
    }

    scm {
        url 'https://github.com/devatherock/jul-jsonformatter/'
    }
}

def projectDescription = 'java.util.logging.Formatter to log messages as JSON, compatible with Logstash'
project.afterEvaluate {
	publishing {
		publications {
			mavenPublication(MavenPublication) {
				from components.java
				artifact sourcesJar {
					classifier "sources"
				}
				artifact javadocJar {
					classifier "javadoc"
				}
				groupId "${project.group}"
				artifactId "${project.name}"
				version "${project.version}"
				pom.withXml {
					def root = asNode()
					root.appendNode('description', projectDescription)
					root.appendNode('name', "${project.name}")
					root.appendNode('url', 'https://github.com/devatherock/jul-jsonformatter')
					root.children().last() + pomConfig
				}
			}
		}
	}
}

/** Publish to bintray **/
bintray {
    user = System.env.BINTRAY_USERNAME
    key = System.env.BINTRAY_KEY
    publications = ['mavenPublication']

    pkg {
        repo = "${project.name}"
        name = "${project.name}"
        userOrg = 'devatherock'
        desc = projectDescription
        websiteUrl = 'https://github.com/devatherock/jul-jsonformatter'
        issueTrackerUrl = 'https://github.com/devatherock/jul-jsonformatter/issues'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/devatherock/jul-jsonformatter.git'
        labels = ['jul', 'logging', 'json', 'logstash']
        githubRepo = 'devatherock/jul-jsonformatter'
        githubReleaseNotesFile = 'README.md'
        
        version {
            desc = projectDescription
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = System.env.SONATYPE_USERNAME
                password = System.env.SONATYPE_PASSWORD
                close = '1'
            }            
        }
        publish = true
        publicDownloadNumbers = true
    }
}
bintrayUpload.dependsOn build

/** Publish to artifactory **/
artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.env.BINTRAY_USERNAME
            password = System.env.BINTRAY_KEY
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(System.env.CIRCLE_BUILD_NUM)
}
artifactoryPublish.dependsOn build
